define mkzas(len){
	@local dispatch, str;
	str = string(len);
	define dispatch args {
		@local arg, range, s, new, beg, end;
		arg = listref(args, 0);

		if(arg == "get"){
			range = listref(args, 1);
			beg = rangebeg(range);
			end = beg+rangelen(range);
			printf("get %x %x\n", beg, end);
			return substr(str, beg, end);
		}

		if(arg == "put"){
			range = listref(args, 1);
			beg = rangebeg(range);
			end = beg+rangelen(range);
			new = listref(args, 2);
			strput(str, beg, new);
			printf("put %x %x\n", beg, end);
			return;
		}

		printf("bad argument to dispatch!\n");
	}
	return mkas(dispatch);
}

define main(){
	@local dom, ns, as, x, y;

	as = mkzas(1024);
	ns = @names c32le {
		struct foo {
			@0x0 int i;
			@0x4 int *p;
			@0x8;
		};
		@0x0	int x;
		@0x4	int y;
		@0x8	struct foo foo;
		@0x10	struct foo a[3];
	};
	dom = mkdom(ns, as);

	x = dom`x++;
	printf("after x=dom`x++: x = %d, dom`x = %d\n", x, dom`x);

	y = ++dom`y;
	printf("after y=++dom`y: y = %d, dom`y = %d\n", y, dom`y);

	x = dom`x--;
	printf("after x=dom`x--: x = %d, dom`x = %d\n", x, dom`x);

	y = --dom`y;
	printf("after y=--dom`y: y = %d, dom`y = %d\n", y, dom`y);
}

main();
