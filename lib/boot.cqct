@global fresh;

@define defaultreturn(arg ...)
{
	printf("exiting l1\n");
	exit(0);
}

@define defaultabort(arg ...)
{
	printf("aborting l1\n");
	exit(1);
}

@define defaulterror(msg)
{
	callcc(@lambda(errk){
			printf("error: %s\n", msg);
			kbacktrace(errk);
			printf("calling defaultreset:\n");
			kbacktrace(defaultreset);
			defaultreset(nil);
	       });
}

@define callfn(fn, args ...)
{
	return callcc(@lambda(k){
			defaultreset = k;
			return apply(fn, args);
		      });
}

@define myeval(s)
{
	@local x, fn;
	x = parse(s);
	fn = compile(x);
	fn();
}

@define repl()
{
	@defloc $loop()
	{
		@local s, v;
		printf("; ");
		s = read(stdin, 1024);
		if(s == nil){
			printf("\n");
			defaultreturn(nil);
		}
		v = myeval(s);
//		v = eval(s);
		if(v != nil)
			printf("%a\n", v);
		$loop();
	}

	callcc(@lambda(done){
			defaultreturn = done;
			callcc(@lambda(reset){
					printf("setting defaultreset to:\n");
					kbacktrace(reset);
					defaultreset = reset;
			       });
			$loop(); 
	       });
}
