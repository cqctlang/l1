@include <linux-x86_64.cqct>
@include <trapman.cqct>

ctlmux = @names clp64le {
	enum Mkind {
		Reserved0 = 0,
		Rerror,
		Reserved2,
		Aevent,
		Tversion,
		Rversion,
		Tping,
		Rping,
		Tps,
		Rps,
		Tlaunch,
		Rlaunch,
		Tattach,
		Rattach,
		Tstat,
		Rstat,
		Tcont,
		Rcont,
		Tstop,
		Rstop,
		Tstep,
		Rstep,
		Tsnap,
		Rsnap,
		Tkill,
		Rkill,
		Tdetach,
		Rdetach,
		Ttrace,
		Rtrace,
		Tsettrap,
		Rsettrap,
		Tclrtrap,
		Rclrtrap,
		Tgetctx,
		Rgetctx,
		Tsetctx,
		Rsetctx,
		Tread,
		Rread,
		Twrite,
		Rwrite,
	};

	enum State {
		Running,
		Stopped,
		Dead,
	};

	enum Mode {
		/* trap modes */
		Mbrk	= 0,
		Msnap	= 1,
	};

	enum Flags {
		/* launch flags */
		Fstdin   = 1,		  /* redirect stdin */
		Fstdout  = (Fstdin<<1),   /* redirect stdout */
		Fstderr  = (Fstdout<<1),  /* redirect stderr */
	};

	typedef
	enum Event {
		Eclear	 = 0,
		Eset	 = 1,
		Esyscall = (Eset<<1),
		Eexec	 = (Esyscall<<1),
		Efork    = (Eexec<<1),
		Eclone   = (Efork<<1),
		Esignal  = (Eclone<<1),
		Eexit    = (Esignal<<1),
		Etrap    = (Eexit<<1),
		Esnap	 = (Etrap<<1),
		Estepctx = (Esnap<<1),
	} Event;

	typedef
	enum Prot {
		PROT_READ	= 0x1,		/* Page can be read.  */
		PROT_WRITE	= 0x2,		/* Page can be written.  */
		PROT_EXEC	= 0x4,		/* Page can be executed.  */
	} Prot;

	typedef
	struct Ctx {
		/* kernel's register set */
		@0x0	uint64 r15;
		@0x8	uint64 r14;
		@0x10	uint64 r13;
		@0x18	uint64 r12;
		@0x20	uint64 rbp;
		@0x28	uint64 rbx;
		@0x30	uint64 r11;
		@0x38	uint64 r10;
		@0x40	uint64 r9;
		@0x48	uint64 r8;
		@0x50	uint64 rax;
		@0x58	uint64 rcx;
		@0x60	uint64 rdx;
		@0x68	uint64 rsi;
		@0x70	uint64 rdi;
		@0x78	uint64 orig_rax;
		@0x80	uint64 rip;
		@0x88	uint64 cs;
		@0x90	uint64 eflags;
		@0x98	uint64 rsp;
		@0xa0	uint64 ss;
		@0xa8	uint64 fs_base;
		@0xb0	uint64 gs_base;
		@0xb8	uint64 ds;
		@0xc0	uint64 es;
		@0xc8	uint64 fs;
		@0xd0	uint64 gs;

		/* current signal number */
		@0xd8	uint64 sig;
		@0xe0; /* 224 */
	} Ctx;

	struct Tversion{
		@0	uint8 op;
		@1	uint32 tag;
		@5;
	};
	typedef struct Tversion Tversion;
	typedef struct Tversion Rversion;

	struct Rerror{
		@0	uint8 op;
		@1	uint32 tag;
		@5	char err[0];
		@5	char bytes[0];
		@5;
	};
	typedef struct Rerror Rerror;
	typedef struct Rerror Rread;

	struct Rcont {
		@0	uint8 op;
		@1	uint32 tag;
		@5;
	};
	typedef struct Rcont Rcont;
	typedef struct Rcont Rclrtrap;
	typedef struct Rcont Rdetach;
	typedef struct Rcont Rkill;
	typedef struct Rcont Rsetctx;
	typedef struct Rcont Rtrace;

	struct Rstop {
		@0	uint8 op;
		@1	uint32 tag;
		@5	Ctx ctx;
		@229;
	};
	typedef struct Rstop Rstop;
	typedef struct Rstop Rgetctx;

	struct Rstep {
		@0	uint8 op;
		@1	uint32 tag;
		@5	Ctx ctx;
		@229;
	};
	typedef struct Rstep Rstep;

	struct Tlaunch{
		@0	uint8 op;
		@1	uint32 tag;
		@5;
	};
	typedef struct Tlaunch Tlaunch;
	typedef struct Tlaunch Tps;
	typedef struct Tlaunch Rps;

	struct Ttrace{
		@0	uint8 op;
		@1	uint32 tag;
		@5	uint64 id;
		@13;
	};
	typedef struct Ttrace Ttrace;

	struct Rlaunch{
		@0	uint8 op;
		@1	uint32 tag;
		@5	uint64 id;
		@13	Ctx ctx;
		@237;
	};
	typedef struct Rlaunch Rlaunch;
	typedef struct Rlaunch Rattach;
	typedef struct Rlaunch Tsetctx;

	struct Tcont{
		@0	uint8 op;
		@1	uint32 tag;
		@5	uint64 id;
		@5	uint64 cnt;
		@13;
	};
	typedef struct Tcont Tcont;
	typedef struct Tcont Tstop;
	typedef struct Tcont Tattach;
	typedef struct Tcont Tkill;
	typedef struct Tcont Tsnap;
	typedef struct Tcont Tdetach;
	typedef struct Tcont Tstep;
	typedef struct Tcont Tstat;
	typedef struct Tcont Rwrite;
	typedef struct Tcont Rping;
	typedef struct Tcont Tgetctx;

	struct Tsettrap{
		@0	uint8 op;
		@1	uint32 tag;
		@5	uint64 id;
		@13;
	};
	typedef struct Tsettrap Tsettrap;

	struct Tclrtrap{
		@0	uint8 op;
		@1	uint32 tag;
		@5	uint64 id;
		@13	uint64 tid;
		@21;
	};
	typedef struct Tclrtrap Tclrtrap;

	struct Rsettrap{
		@0	uint8 op;
		@1	uint32 tag;
		@5	uint64 tid;
		@13;
	};
	typedef struct Rsettrap Rsettrap;

	struct Rsnap{
		@0	uint8 op;
		@1	uint32 tag;
		@5	uint64 cid;
		@13	Ctx ctx;
		@237;
	};
	typedef struct Rsnap Rsnap;

	struct Aevent{
		@0	uint8 op;
		@1	uint32 tag;
		@5	uint64 id;
		@13	uint64 flags;
		@21;
	};
	typedef struct Aevent Aevent;

	struct Esyscall {
		@0	Ctx ctx;
		@224;
	};
	typedef struct Esyscall Esyscall;

	struct Eexit {
		@0;
	};
	typedef struct Eexit Eexit;

	struct Etrap {
		@0	uint64 tid;
		@8	Ctx ctx;
		@232;
	};
	typedef struct Etrap Etrap;
	typedef struct Etrap Eexec;

	struct Esnap {
		@0	uint64 tid;
		@8	uint64 cid;
		@16	Ctx ctx;
		@240;
	};
	typedef struct Esnap Esnap;

	struct Esignal {
		@0	Ctx ctx;
		@224;
	};
	typedef struct Esignal Esignal;

	struct Efork {
		@0	uint64 cid;
		@8	Ctx ctx;
		@232	Ctx cctx;
		@456;
	};
	typedef struct Efork Efork;
	typedef struct Efork Eclone;

	struct Tread{
		@0	uint8 op;
		@1	uint32 tag;
		@5	uint64 id;
		@13	uint64 fd;
		@21     uint64 addr;
		@29	uint64 cnt;
		@37;
	};
	typedef struct Tread Tread;

	struct Twrite{
		@0	uint8 op;
		@1	uint32 tag;
		@5	uint64 id;
		@13	uint64 fd;
		@21     uint64 addr;
		@29	char bytes[0];
		@29;
	};
	typedef struct Twrite Twrite;

	struct Rstat{
		@0	uint8 op;
		@1	uint32 tag;
		@5	char bytes[0];
		@5;
	};
	typedef struct Rstat Rstat;
	typedef struct Rstat Tping;
};

@record region {
	file,
	addr,
	len,
	flags,
};

@record ctlrec {
	id,
	xstop,
	xcont,
	xstep,
	detach,
	kill,
	snap,
	mem,
	reg,
	setreg,
	trace,
	traces,
	xtrap,
	xsnap,
	xtraps,
	trapdel,
	trapreset,
	// private
	state,
	traps,
	callbacks,
	pending,
};

@define mkctl(mux, id, state, _reg, traps, callbacks)
{
	@local xstop, xcont, xstep, detach, kill, snap;
	@local mem, reg, setreg, trace, traces, xtrap, xsnap, xtraps;
	@local trapset, trapdel, trapreset;
	@local this;

	@define xstop()
	{
		if(this.pending == nil)
			return mux.stop(this);
		this.pending = "stop";
		return nil;
	}

	@define xcont()
	{
		if(this.pending == nil)
			return mux.cont(this);
		this.pending = "run";
		return nil;
	}

	@define xstep()
	{
		if(this.pending == nil)
			return mux.step(this);
		this.pending = "step";
		return nil;
	}

	@define detach()
	{
		if(this.pending == nil)
			mux.detach(this);
		this.pending = "detach";
		return nil;
	}

	@define kill()
	{
		if(this.pending == nil)
			mux.kill(this);
		this.pending = "kill";
		return nil;
	}

	@define snap()
	{
		return mux.snap(this);
	}

//	@define traces() { return mux.traces(this); }
//	@define xtraps() { return mux.xtraps(this); }

	@define trace(event, fn)
	{
		callbacks[(ctlmux`Event)event] = fn;
		if(fn == nil)
			return mux.trace(this, event|ctlmux`Eclear);
		else
			return mux.trace(this, event|ctlmux`Eset);
	}

	@define trapset(addr, fn, mode)
	{
		@local tid, bp, kind;
		bp = mux.settrap(this, mode, addr);
		switch(mode){
		case ctlmux`Mbrk:
			kind = "brk";
			break;
		case ctlmux`Msnap:
			kind = "snap";
			break;
		default:
			error("unknown trap mode: %e", mode);
		}
		tid = traps.add(bp, fn, [kind, addr], nil);
		return tid;
	}

	@define xtrap(addr, fn)
	{
		return trapset(addr, fn, ctlmux`Mbrk);
	}

	@define xsnap(addr, fn)
	{
		return trapset(addr, fn, ctlmux`Msnap);
	}

	@define xtraps()
	{
		return traps.list();
	}

	@define trapdel(tid)
	{
		@local trap;
		/* FIXME: this api sucks */
		trap = traps.lookuid(tid);
		traps.clr(tid);
		mux.clrtrap(this, trap.mid);
		return nil;
	}

	@define trapreset()
	{
		traps.reset();
		return nil;
	}

	@define setreg(r)
	{
		_reg = r;
	}

	@define reg()
	{
		return _reg;
	}

	@define mem()
	{
		@local map, get, put, prot2str, dispatch, ctl, id;

		@define get(as, r)
		{
			return mux.pread(this, rangebeg(r), rangelen(r));
		}

		@define put(as, r, s)
		{
			return mux.pwrite(this,  rangebeg(r), rangelen(r), s);
		}

		@define prot2str(prot)
		{
			@local s;
			prot = (ctlmux`Prot)prot;
			s = "";
			if(prot&ctlmux`PROT_READ)
				s += "r";
			else
				s += "-";
			if(prot&ctlmux`PROT_WRITE)
				s += "w";
			else
				s += "-";
			if(prot&ctlmux`PROT_EXEC)
				s += "x";
			else
				s += "-";
			return s;
		}

		@define map(as)
		{
			@local n, rs, i;

			rs = mux.pmap(this);
			n = length(rs);
			for(i = 0; i < n; i++){
				printf("%016x %016x %s %s\n",
				       rs[i].addr,
				       rs[i].addr+rs[i].len,
				       prot2str(rs[i].flags),
				       rs[i].file);
			}
			return vector(mkrange(0, (`uint64)-1));
		}

		@define ctl(as)
		{
			return this;
		}


		@define id(as)
		{
			return this.id;
		}

		// redirect unknown calls to ctl
		@define dispatch(arg ...)
		{
			@local name, f, rv;
			pop(arg);
			name = pop(arg);

			// this is not cool.
			f = rdgettab(rdof(this))[name](this);

			rv = apply(f, arg);
			return rv;
		}

		return mkas([ "get"       : get,
			      "put"       : put,
			      "map"       : map,
			      "ctl"       : ctl,
			      "id"	  : id,
			      "dispatch"  : dispatch,
			    ]);
	}

	this = ctlrec(id, xstop, xcont, xstep, detach, kill, snap,
		      mem, reg, setreg, trace, traces, xtrap, xsnap, xtraps,
		      trapdel, trapreset,
		      state, traps, callbacks, nil);
	return this;
}

@record muxrec {
	ps,
	launch,
	attach,
	run,
	abort,
	// private
	stop,
	cont,
	step,
	detach,
	kill,
	snap,
	pread,
	pwrite,
	pmap,
	trace,
	settrap,
	clrtrap,
};

@define mkctlmux_local()
{
	@local fds, fd;
	fds = popen("prctl", "-c", 2|4|8);
	fd = fds[0];
	finalize(fd, @lambda(fd) { close(fd); });
	return mkctlmux(fds[0]);
}

@define mkctlmux_remote(address)
{
	@local fd;
	fd = tcpopen(address);
	finalize(fd, @lambda(fd) { close(fd); });
	return mkctlmux(fd);
}

@define mkctlmux(fd)
{
	@local tab, launch, attach, run, abort;
	@local logsend, logrecv, log, verbose;
	@local checkreply, cop, ctls;
	@local freetags, nexttag, taghandler, mux;
	@local stop, step, detach, kill, snap;
	@local pread, pwrite, pmap;
	@local trace, fmtmsg, buf2msg;
	@local settrap, clrtrap;
	@local aborted, version, ping, ps;

	verbose = 0;
	aborted = 0;
	nexttag = 0;
	freetags = [ ];
	ctls = [:];
	taghandler = [:];

	@define freshtag()
	{
		if(isempty(freetags))
			return nexttag++;
		return pop(freetags);
	}

	@define reusetag(tag)
	{
		push(freetags, tag);
	}

	@define logsend(s)
	{
		if(verbose)
			printf("<-- %s\n", s);
	}
	@define logrecv(s)
	{
		if(verbose)
			printf("--> %s\n", s);
	}
	@define log(arg ...)
	{
		if(verbose){
			apply(printf, arg);
			printf("\n");
		}
	}

	@define cop(p)
	{
		return {@typeof(p)}getbytes(p);
	}

	@define checkreply(rep, op)
	{
		switch(rep->op){
		case op:
			break;
		case ctlmux`Rerror:
			error("ctlmux: while waiting for %e: %s",
			      op, rep->bytes);
		default:
			error("ctlmux: while waiting for %e: received %e",
			      op, (enum ctlmux`Mkind)rep->op);
		}
	}

	@define fmtmsg(msg)
	{
		return sprintfa("%e %d", (enum ctlmux`Mkind)msg->op, msg->id);
	}

	@define buf2msg(p)
	{
		@local op;
		op = (enum ctlmux`Mkind)*(ctlmux`uint8*)p;
		switch(op){
		case ctlmux`Aevent:
			return (ctlmux`Aevent*)p;
		case ctlmux`Rerror:
			return (ctlmux`Rerror*)p;
		case ctlmux`Tversion:
			return (ctlmux`Tversion*)p;
		case ctlmux`Rversion:
			return (ctlmux`Rversion*)p;
		case ctlmux`Tps:
			return (ctlmux`Tps*)p;
		case ctlmux`Rps:
			return (ctlmux`Rps*)p;
		case ctlmux`Tlaunch:
			return (ctlmux`Tlaunch*)p;
		case ctlmux`Rlaunch:
			return (ctlmux`Rlaunch*)p;
		case ctlmux`Tcont:
			return (ctlmux`Tcont*)p;
		case ctlmux`Rcont:
			return (ctlmux`Rcont*)p;
		case ctlmux`Tstop:
			return (ctlmux`Tstop*)p;
		case ctlmux`Rstop:
			return (ctlmux`Rstop*)p;
		case ctlmux`Tkill:
			return (ctlmux`Tkill*)p;
		case ctlmux`Rkill:
			return (ctlmux`Rkill*)p;
		case ctlmux`Tdetach:
			return (ctlmux`Tdetach*)p;
		case ctlmux`Rdetach:
			return (ctlmux`Rdetach*)p;
		case ctlmux`Tsettrap:
			return (ctlmux`Tsettrap*)p;
		case ctlmux`Rsettrap:
			return (ctlmux`Rsettrap*)p;
		case ctlmux`Tclrtrap:
			return (ctlmux`Tclrtrap*)p;
		case ctlmux`Rclrtrap:
			return (ctlmux`Rclrtrap*)p;
		case ctlmux`Tread:
			return (ctlmux`Tread*)p;
		case ctlmux`Rread:
			return (ctlmux`Rread*)p;
		case ctlmux`Tsnap:
			return (ctlmux`Tsnap*)p;
		case ctlmux`Rsnap:
			return (ctlmux`Rsnap*)p;
		case ctlmux`Twrite:
			return (ctlmux`Twrite*)p;
		case ctlmux`Rwrite:
			return (ctlmux`Rwrite*)p;
		case ctlmux`Tgetctx:
			return (ctlmux`Tgetctx*)p;
		case ctlmux`Rgetctx:
			return (ctlmux`Rgetctx*)p;
		case ctlmux`Tsetctx:
			return (ctlmux`Tsetctx*)p;
		case ctlmux`Rsetctx:
			return (ctlmux`Rsetctx*)p;
		case ctlmux`Tattach:
			return (ctlmux`Tattach*)p;
		case ctlmux`Rattach:
			return (ctlmux`Rattach*)p;
		case ctlmux`Tping:
			return (ctlmux`Tping*)p;
		case ctlmux`Rping:
			return (ctlmux`Rping*)p;
		case ctlmux`Tstep:
			return (ctlmux`Tstep*)p;
		case ctlmux`Rstep:
			return (ctlmux`Rstep*)p;
		case ctlmux`Ttrace:
			return (ctlmux`Ttrace*)p;
		case ctlmux`Rtrace:
			return (ctlmux`Rtrace*)p;
		case ctlmux`Tstat:
			return (ctlmux`Tstat*)p;
		case ctlmux`Rstat:
			return (ctlmux`Rstat*)p;
		default:
			error("unknown op %e", op);
		}
	}

	@define isasync(msg)
	{
		switch((enum ctlmux`Mkind)msg->op){
		case ctlmux`Aevent:
			return 1;
		default:
			return 0;
		}
	}

	@define sendmsg(fd, arg ...)
	{
		@local p, sz;
		p = arg[0];
		switch(length(arg)){
		case 1:
			sz = sizeof(*p);
			break;
		case 2:
			sz = arg[1];
			break;
		default:
			error("bad sendmsg");
		}
		write(fd, cval2str((ctlmux`uint64)sz));
		write(fd, getbytes(p, sz));
	}

	@define handleasync(msg, dat)
	{
		@local ctl, newctl, trap, ev, fn, docall;
		ctl = ctls[msg->id];
		if(ctl == nil)
			error("%e:%e for unknown process id: %d",
			      (enum ctlmux`Mkind)msg->op,
			      (ctlmux`Event)msg->flags,
			      msg->id);
		if((enum ctlmux`Mkind)msg->op != ctlmux`Aevent)
			error("ctlmux: unhandled message %e",
			      (enum ctlmux`Mkind)msg->op);

		@define docall(ctls, thunk)
		{
			foreach(@lambda(ctl){
					ctl.pending = "run";
				}, ctls);
			thunk();
			foreach(@lambda(ctl){
					@local p;
					p = ctl.pending;
					ctl.pending = nil;
					// FIXME: do all of these async
					// to avoid stack explosion
					switch(p){
					case "run":
						contasync(ctl);
						break;
					case "stop":
						/* already stopped */
						break;
					case "kill":
						mux.kill(ctl);
						break;
					case "step":
						mux.step(ctl);
						break;
					case "detach":
						mux.detach(ctl);
						break;
					default:
						error("unexpected pending %a",
						      p);
					}
				}, ctls);
		}

		ev = (ctlmux`Event)msg->flags;
		switch(ev){
		case ctlmux`Etrap:
			dat = (ctlmux`Etrap*)dat;
			trap = ctl.traps.look(dat->tid);
			if(trap == nil)
				error("ctl %d has no trap for tid %d",
				      ctl.id, dat->tid);
			ctl.state = ctlmux`Stopped;
			ctl.setreg(cop(&dat->ctx));
			docall([ctl], @lambda() { trap.fn(ctl); });
			break;
		case ctlmux`Esnap:
			dat = (ctlmux`Esnap*)dat;
			trap = ctl.traps.look(dat->tid);
			if(trap == nil)
				error("ctl %d has no trap for tid %d",
				      ctl.id, dat->tid);
			newctl = mkctl(mux,
				       dat->cid,
				       ctlmux`Stopped,
				       cop(&dat->ctx),
				       ctl.traps.copy(),
				       ctl.callbacks);
			ctls[dat->cid] = newctl;
			docall([ctl], /* no control of newctl */
			       @lambda() { trap.fn(ctl, newctl); });
			break;
		case ctlmux`Eclone:
		case ctlmux`Efork:
			dat = (ctlmux`Efork*)dat;
			log("task %d has %s %d",
			    msg->id,
			    ev == ctlmux`Eclone ? "cloned" : "forked",
			    dat->cid);
			ctl.state = ctlmux`Stopped;
			ctl.setreg(cop(&dat->ctx));
			newctl = mkctl(mux,
				       dat->cid,
				       ctlmux`Stopped,
				       cop(&dat->cctx),
				       ctl.traps.copy(),
				       copy(ctl.callbacks));
			ctls[dat->cid] = newctl;
			fn = ctl.callbacks[ev];
			if(fn != nil)
				docall([ctl, newctl],
				       @lambda() { fn(ctl, newctl); });
			else{
				contasync(ctl);
				contasync(newctl);
			}
			break;
		case ctlmux`Eexit:
			log("task %d has died", msg->id);
			fn = ctl.callbacks[ev];
			if(fn != nil)
				// it's dead: nothing to control with docall
				fn(ctl);
			tabdelete(ctls, msg->id);
			break;
		case ctlmux`Eexec:
			dat = (ctlmux`Eexec*)dat;
			ctl.state = ctlmux`Stopped;
//			ctl.setreg(cop(&dat->ctx));
			log("task %d has called exec", msg->id);
			fn = ctl.callbacks[ev];
			if(fn != nil)
				docall([ctl], @lambda() { fn(ctl); });
			else
				contasync(ctl);
			break;
		case ctlmux`Esyscall:
			dat = (ctlmux`Esyscall*)dat;
			ctl.state = ctlmux`Stopped;
			ctl.setreg(cop(&dat->ctx));
			log("task %d has hit a system call trap (%e)",
			    msg->id,
			    (enum linuxnames`syscall)dat->ctx.orig_rax);
			fn = ctl.callbacks[ev];
			if(fn != nil)
				docall([ctl], @lambda() { fn(ctl); });
			else
				contasync(ctl);
			break;
		case ctlmux`Esignal:
			dat = (ctlmux`Esignal*)dat;
			ctl.state = ctlmux`Stopped;
			fn = ctl.callbacks[ev];
			if(fn != nil)
				docall([ctl],
				       @lambda() {
					       fn(ctl, dat->ctx.sig);
				       });
			else
				contasync(ctl);
			break;
		default:
			error("unhandled event message: %e",
			      (ctlmux`Event)msg->flags);
 		}
	}

	@define noctls()
	{
		return length(ctls) == 0;
	}

	@define noinflight()
	{
		return length(taghandler) == 0;
	}

	@define runtil(pred)
	{
		@local s, sz, p, op, msg, h, tag, dat;

		while(!aborted && !pred()){
			if(noinflight() && noctls())
				error("runtil: nothing to do");
			s = read(fd, sizeof(ctlmux`uint64));
			if(s == nil)
				error("remote prctl hung up");
			sz = *(ctlmux`uint64*)s;
			p = read(fd, sz);
			msg = buf2msg(p);
			if(sz > sizeof(*msg))
				dat = getbytes(msg+1, sz-sizeof(*msg));
			else
				dat = "";

			if(isasync(msg)){
				handleasync(msg, dat);
				continue;
			}

			tag = msg->tag;
			h = taghandler[tag];
			tabdelete(taghandler, tag);
			reusetag(tag);
			if(h == nil){
				log("unhandled tag %d: %s", tag, fmtmsg(msg));
				continue;
			}
			h(msg, dat);
		}
	}

	@define runtiltag(tag)
	{
		@local done, msg, dat;
		msg = nil;
		done = 0;
		taghandler[tag] = @lambda(m, d) { msg = m; dat = d; done = 1; };
		runtil(@lambda(){ done; });
		return [msg, dat];
	}

	@define clrtrap(ctl, tid)
	{
		@local p, rep, dat;
		p = (ctlmux`Tclrtrap*)malloc(sizeof(ctlmux`Tclrtrap));
		p->op = ctlmux`Tclrtrap;
		p->tag = freshtag();
		p->id = ctl.id;
		p->tid = tid;
		sendmsg(fd, p);
		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rclrtrap);
		return nil;
	}

	@define settrap(ctl, mode, addr)
	{
		@local p, rep, dat, clear, sz, q;
		sz = sizeof(ctlmux`Tsettrap)+2*sizeof(ctlmux`uint64);
		p = (ctlmux`Tsettrap*)malloc(sz);
		p->op = ctlmux`Tsettrap;
		p->tag = freshtag();
		p->id = ctl.id;
		q = (ctlmux`uint64*)(p+1);
		*q++ = mode;
		*q++ = addr;
		sendmsg(fd, p, sz);
		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rsettrap);
		return rep->tid;
	}

	@define trace(ctl, flags)
	{
		@local p, rep, dat, sz, q;

		sz = sizeof(ctlmux`Ttrace)+sizeof(ctlmux`uint64);
		p = (ctlmux`Ttrace*)malloc(sz);
		p->op = ctlmux`Ttrace;
		p->tag = freshtag();
		p->id = ctl.id;
		q = (ctlmux`uint64*)(p+1);
		*q++ = flags;
		sendmsg(fd, p, sz);
		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rtrace);
	}

	@define pread(ctl, addr, cnt)
	{
		@local p, rep, dat;

		p = (ctlmux`Tread*)malloc(sizeof(ctlmux`Tread));
		p->op = ctlmux`Tread;
		p->tag = freshtag();
		p->id = ctl.id;
		p->fd = -1;
		p->addr = addr;
		p->cnt = cnt;
		sendmsg(fd, p);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rread);
		return dat;
	}

	@define pwrite(ctl, addr, cnt, bytes)
	{
		@local sz, p, rep, dat;

		sz = sizeof(ctlmux`Twrite)+cnt;
		p = (ctlmux`Twrite*)malloc(sz);
		p->op = ctlmux`Twrite;
		p->tag = freshtag();
		p->id = ctl.id;
		p->fd = -1;
		p->addr = addr;
		putbytes(p+1, bytes);
		sendmsg(fd, p, sz);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rwrite);
		return nil;
	}

	@define pmap(ctl)
	{
		@local p, rep, dat, q, i, m, sz, rs, file, addr, len, flags;

		p = (ctlmux`Tstat*)malloc(sizeof(ctlmux`Tstat));
		p->op = ctlmux`Tstat;
		p->tag = freshtag();
		p->id = ctl.id;
		sendmsg(fd, p);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rstat);

		rs = [];
		q = (ctlmux`uint64*)dat;
		q++; /* skip pid */
		m = *q++;
		for(i = 0; i < m; i++){
			sz = *q++;
			file = getbytes(q, sz);
			q = (ctlmux`uint64*)((char*)q+sz);
			addr = *q++;
			len = *q++;
			flags = *q++;
			append(rs, region(file, addr, len, flags));
		}
		return rs;
	}

	@define contasync(ctl)
	{
		@local p;

		if(ctl.state != ctlmux`Stopped)
			return;
		p = (ctlmux`Tcont*)malloc(sizeof(ctlmux`Tcont));
		p->op = ctlmux`Tcont;
		p->tag = freshtag();
		p->id = ctl.id;
		sendmsg(fd, p);

		taghandler[p->tag] = @lambda(rep, dat){
			checkreply(rep, ctlmux`Rcont);
			ctl.state = ctlmux`Running;
		};
	}

	@define cont(ctl)
	{
		@local p, rep, dat;

		if(ctl.state != ctlmux`Stopped)
			return;
		p = (ctlmux`Tcont*)malloc(sizeof(ctlmux`Tcont));
		p->op = ctlmux`Tcont;
		p->tag = freshtag();
		p->id = ctl.id;
		sendmsg(fd, p);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rcont);
		ctl.state = ctlmux`Running;
		return nil;
	}

	@define stop(ctl)
	{
		@local p, rep, dat;

		if(ctl.state != ctlmux`Running)
			return;
		p = (ctlmux`Tcont*)malloc(sizeof(ctlmux`Tstop));
		p->op = ctlmux`Tstop;
		p->tag = freshtag();
		p->id = ctl.id;
		sendmsg(fd, p);

		/* FIXME: consume registers */
		[rep, dat] = runtiltag(p->tag);
		ctl.setreg(cop(&rep->ctx));
		checkreply(rep, ctlmux`Rstop);
		ctl.state = ctlmux`Stopped;
		return nil;
	}

	@define step(ctl)
	{
		@local p, rep, dat;

		if(ctl.state != ctlmux`Stopped)
			error("attempt to step running ctl");
		p = (ctlmux`Tcont*)malloc(sizeof(ctlmux`Tstep));
		p->op = ctlmux`Tstep;
		p->tag = freshtag();
		p->id = ctl.id;
		sendmsg(fd, p);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rstep);
		ctl.setreg(cop(&rep->ctx));
		return nil;
	}

	@define detach(ctl)
	{
		@local p, rep, dat;

		p = (ctlmux`Tdetach*)malloc(sizeof(ctlmux`Tdetach));
		p->op = ctlmux`Tdetach;
		p->tag = freshtag();
		p->id = ctl.id;
		sendmsg(fd, p);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rdetach);
		ctl.state = ctlmux`Dead;
		log("task %d has detached", ctl.id);
		tabdelete(ctls, ctl.id);
	}

	@define kill(ctl)
	{
		@local p, rep, dat;

		p = (ctlmux`Tkill*)malloc(sizeof(ctlmux`Tkill));
		p->op = ctlmux`Tkill;
		p->tag = freshtag();
		p->id = ctl.id;
		sendmsg(fd, p);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rkill);
		tabdelete(ctls, ctl.id);
		return nil;
	}

	@define snap(ctl)
	{
		@local p, rep, dat, newctl;

		p = (ctlmux`Tsnap*)malloc(sizeof(ctlmux`Tsnap));
		p->op = ctlmux`Tsnap;
		p->tag = freshtag();
		p->id = ctl.id;
		sendmsg(fd, p);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rsnap);
		newctl = mkctl(mux,
			       rep->cid,
			       ctlmux`Stopped,
			       cop(&rep->ctx),
			       ctl.traps.copy(),
			       copy(ctl.callbacks));
		ctls[rep->cid] = newctl;
		return newctl;
	}

	@define launch(args, flags)
	{
		@local narg, s, sz, i, p, q, ctl, rep, dat;

		narg = length(args);
		if(narg == 0)
			error("mkpras: launch: empty command");
		if(flags == nil)
			flags = 0;

		/* format and send Tlaunch */
		sz = sizeof(ctlmux`Tlaunch);
		sz += sizeof(ctlmux`uint64); /* flags */
		sz += sizeof(ctlmux`uint64); /* narg */
		for(i = 0; i < narg; i++)
			sz += sizeof(ctlmux`uint64)+strlen(args[i]);
		p = (ctlmux`Tlaunch*)malloc(sz);
		p->op = ctlmux`Tlaunch;
		p->tag = freshtag();
		q = (ctlmux`uint64*)(p+1);
		*q++ = flags;
		*q++ = narg;
		for(i = 0; i < narg; i++){
			s = args[i];
			q = (ctlmux`uint64*)q;
			*q++ = strlen(s);
			q = (char*)q;
			putbytes(q, s);
			q += strlen(s);
		}
		sendmsg(fd, p, sz);

		/* wait for reply */
		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rlaunch);
		ctl = mkctl(mux,
			    rep->id,
			    ctlmux`Stopped,
			    (ctlmux`Ctx*)cop(&rep->ctx),
			    mktrapman(),
			    [ : ]);
		ctls[rep->id] = ctl;
		return ctl;
	}

	@define attach(pid)
	{
		@local p, rep, dat, ctl;

		p = (ctlmux`Tattach*)malloc(sizeof(ctlmux`Tattach));
		p->op = ctlmux`Tattach;
		p->tag = freshtag();
		p->id = pid;
		sendmsg(fd, p);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rattach);
		ctl = mkctl(mux,
			    rep->id,
			    ctlmux`Stopped,
			    (ctlmux`Ctx*)cop(&rep->ctx),
			    mktrapman(),
			    [ : ]);
		ctls[rep->id] = ctl;
		return ctl;
	}

	@define version()
	{
		@local p, rep, dat, sz, version, os, s;

		version = "prctl-2010:x86-linux-2010,x86-win-2010";

		sz = sizeof(ctlmux`Tversion)+strlen(version);
		p = (ctlmux`Tversion*)malloc(sz);
		p->op = ctlmux`Tversion;
		p->tag = freshtag();
		putbytes(p+1, version);
		sendmsg(fd, p, sz);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rversion);
		s = split(dat, ":");
		if(length(s) != 2)
			error("unknown remote prctl version: %s", dat);
		os = s[1];
		os = substr(os, 0, strlen(os)); /* chop NUL */
	}

	@define ping(cnt)
	{
		@local p, rep, dat, sz;

		sz = sizeof(ctlmux`Tping)+cnt;
		p = (ctlmux`Tping*)malloc(sz);
		p->op = ctlmux`Tping;
		p->tag = freshtag();
		sendmsg(fd, p, sz);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rping);
		if(rep->cnt != cnt)
			error("ping reply acknowledged %d bytes, not %d",
			      rep->cnt, cnt);
		printf("prctl ping: %d bytes\n", cnt);
	}

	@define ps()
	{
		@local p, rep, dat, sz, i, j, np, nt, q, pid, tid, cmd;
		@local proc, rv;

		sz = sizeof(ctlmux`Tps);
		p = (ctlmux`Tps*)malloc(sz);
		p->op = ctlmux`Tps;
		p->tag = freshtag();
		sendmsg(fd, p, sz);

		[rep, dat] = runtiltag(p->tag);
		checkreply(rep, ctlmux`Rps);
		q = (ctlmux`uint64*)dat;
		np = *q++;
		rv = [];
		for(i = 0; i < np; i++){
			proc = [];
			pid = *q++;
			append(proc, pid);
			sz = *q++;
			cmd = getbytes(q, sz);
			append(proc, cmd);
			q = (ctlmux`uint64*)((char*)q+sz);
			printf("\t%5d\t%s", pid, cmd);
			nt = *q++;
			for(j = 0; j < nt; j++){
				tid = *q++;
				append(proc, tid);
				printf(" %5d", tid);
			}
			append(rv, proc);
			printf("\n");
		}
		return rv;
	}

	@define abort()
	{
		close(fd);
		aborted = 1;
		return nil;
	}

	@define run()
	{
		if(aborted)
			error("attempt to resume aborted mux");
		runtil(noctls);
		return nil;
	}

	version();
	mux = muxrec(ps,
		     launch,
		     attach,
		     run,
		     abort,
		     stop,
		     cont,
		     step,
		     detach,
		     kill,
		     snap,
		     pread,
		     pwrite,
		     pmap,
		     trace,
		     settrap,
		     clrtrap);
	return mux;
}
