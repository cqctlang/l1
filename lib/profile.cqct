

@defstx @init_profile() {
	@global profiles, profentries;
	profiles = mktab(@lambda() {
			 	return [0,  // count
					0,  // time
					0,  // expansions
					nil]; // location
			 });
	profentries = [:];
	return #[];
}
@init_profile();

@defstx @profile(rest ...) body {

	@local ssym, name, src;
	
	src = stxsrc(body);
		
	if(rest[0]) {
		name = rest[0];
		if(stxkind(name) != 'val)
			error("@profile: arg 0 must be a value");
	} else {
		src = stxsrc(body);
		if(isnil(src[1]))
			name = sprintfa("%s",src[0]);
		else
			name = sprintfa("%s:%d",src[0],src[1]);
		name=mkstxval(name);
	}

	if(stxkind(body[0][0]) != 'null) {
		error("profile body is not a scope. no top-level bindings allowed.");	
	}

	ssym=mkstxval(stxid(gensym()));

	profiles[stxval(name)][2]++;
	profiles[stxval(name)][3] = src;

	return #`{
		profentries[#,(ssym)]=gettimeofday();
		// just the elist, not the scope
		#,(body[0][1]);
		{
			@local t,l;
	
			t = profiles[#,(name)];
			l = profentries[#,(ssym)];
			t[0]++;
			if(!isnil(l))
				t[1] += gettimeofday()-l;
			else
				fprintf(stderr,"@profile: accounting disrupted by non-local entry\n");
		}
	};
}
