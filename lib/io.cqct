@global
	fdopen,
	mapfile,      
	open
;	

{
@local ions;

ions = @names c32le {
	enum Fflag {
		/* FFlag in syscqct.h */
		Fread = 4,
		Fwrite = 8,
	};
};

@defloc mode2flags(mode)
{
	@local flags;
	flags = 0;
	if(strstr(mode, "r") != nil)
		flags |= ions`Fread;
	if(strstr(mode, "w") != nil)
		flags |= ions`Fwrite;
	return flags;
}

@define open(file, mode)
{
	@local fd, rv;

	fd = _open(file, mode);
	if(fd == nil)
		return nil;
	rv = mksysfd(fd, mode2flags(mode), file);
	finalize(rv, @lambda(fd){ close(fd); });
	return rv;
}

@define fdopen(fd, mode)
{
	@local rv;
	rv = mksysfd(fd, mode2flags(mode), sprintfa("fd%d", fd));
	finalize(rv, @lambda(fd){ close(fd); });
	return rv;
}

@define mapfile(arg ...)
{
	@local addr, len, rv;
	[addr, len] = apply(_mapfile, arg);
	rv = mkstrm(addr, len);
	finalize(rv, @lambda(x){ _munmap(addr, len); });
	return rv;
}

}
